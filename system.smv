
------------------------------------- Bus ------------------------------------
MODULE Bus
VAR
  -------------- bus out data --------------
  AD:      {0,1}; -- address
  DT:      boolean; -- data if CTRL is write
  CTRL:    {read, write};
  arb_gnt: {none, l1_1, mem}; -- arbitre decision
  valid:   boolean; -- data on the bus is valid

  ------------- computed state -------------
  is_valid_mem:  boolean;
  is_valid_cache: boolean;

ASSIGN
  is_valid_mem := arb_gnt = mem & valid;
  is_valid_cache := arb_gnt = l1_1 & valid;

----------------------------------- Memory -----------------------------------
MODULE Memory(bus)
VAR
  ------------- internal values ------------
  data: array 0..1 of boolean; -- internal data
  --------------- out values ---------------
  AD:    {0,1}; -- requested adress
  DT:    boolean; -- response to bus
  CTRL:  {read, write};
  valid: boolean; -- if data is valid

ASSIGN
  init(data[0]) := FALSE;
  init(data[1]) := TRUE;
  init(AD)      := 0;
  init(DT)      := FALSE;
  init(CTRL)    := read;
  init(valid)   := FALSE;

  ------------- internal values ------------
  next(data[0]) :=
    case
      bus.is_valid_cache & bus.CTRL = write & bus.AD = 0: bus.DT;
      TRUE:                                               data[0];
    esac;
  next(data[1]) :=
    case
      bus.is_valid_cache & bus.CTRL = write & bus.AD = 1: bus.DT;
      TRUE:                                               data[1];
    esac;
  --------------- out values ---------------
  next(AD) :=
    case
      bus.valid: bus.AD;
      TRUE: AD;
    esac;
  next(DT) :=
    case
      bus.valid & bus.CTRL = read: data[bus.AD];
      TRUE: DT;
    esac;
  next(CTRL) :=
    case
      bus.valid: bus.CTRL;
      TRUE: CTRL;
    esac;
  next(valid) :=
    case
      bus.arb_gnt = mem & bus.DT = DT: TRUE; -- if bus out data is mem data, then valid is TRUE
      TRUE:                            FALSE; -- else valid is false
    esac;

------------------------------------ Cache -----------------------------------
MODULE Cache(bus, processor, id)
VAR
  ------------- internal values ------------
  TAG: {0,1}; -- cached value tag
  REG: boolean; -- cached value
  --------------- out values ---------------
  AD:   {0,1}; -- address
  DT:   boolean; -- data if CTRL is write
  CTRL: {read, write};
  valid:boolean; -- if data is valid
  req:  boolean; -- cache request for bus

  ------------- Computed state -------------
  is_our_request: boolean;
  is_our_response: boolean;

ASSIGN
  is_our_request := bus.AD = AD & bus.CTRL = CTRL;
  is_our_response := req = TRUE & bus.is_valid_mem & is_our_request;

  init(DT)   := FALSE;
  init(CTRL) := read;
  init(req)  := FALSE;

  ------------- proc out values ------------
  processor.DT := DT; -- proc out data is data taken from bus
  next(processor.valid) :=
    case
      is_our_response : TRUE; -- proc out is valid after the data on the bus is our response
      TRUE:             FALSE; -- default is not valid
    esac;
  ------------- bus out values -------------
  AD := processor.AD;
  valid := bus.arb_gnt = id & req;
  next(DT) :=
    case
      processor.req = load & is_our_response: bus.DT;
      processor.req = store: processor.REG;
      TRUE: DT;
    esac;
  next(CTRL) :=
    case
      processor.req = load: read;
      processor.req = store: write;
      TRUE: CTRL;
    esac;
  next(req) :=
    case
      is_our_response :      FALSE;
      processor.req != idle & !processor.valid: TRUE;
      TRUE:                  req;
    esac;

---------------------------------- Processor ---------------------------------
MODULE Processor
VAR
  ------------- cache out values -----------
  req:   {idle, load, store}; -- processor request to cache
  AD:    {0,1}; -- requested address
  ------------- internal values ------------
  REG:   boolean; -- proc data
  ------------- cache in values ------------
  DT:    boolean; -- data from l1
  valid: boolean; -- if data is valid

ASSIGN
  init(AD)    := 0;
  init(REG)   := FALSE;
  init(valid) := FALSE;

  ------------- cache out values -----------
  next(req) :=
    case
      valid: {idle, load, store};
      req = idle: {idle, load, store};
      TRUE: req;
    esac;
  next(AD) :=
    case
      valid: {0,1};
      TRUE: AD;
    esac;
  ------------- internal values ------------
  next(REG) :=
    case
      valid: {TRUE,FALSE};
      TRUE: REG;
    esac;

------------------------------------ Main ------------------------------------
MODULE main
VAR
  bus: Bus;
  processor: Processor;
  cache: Cache(bus, processor, l1_1);
  memory: Memory(bus);

ASSIGN
  -------------- init values ---------------
  init(bus.arb_gnt) := none;

  -------------- bus arbitre ---------------
  next(bus.arb_gnt) :=
    case
      cache.req & (bus.is_valid_mem | bus.arb_gnt = none) : l1_1; -- if mem data is valid on the bus, then next master is l1_1
      bus.is_valid_cache | bus.arb_gnt = mem:               mem; -- if cache made a request, then next master is mem
      TRUE:                                                 none; -- default bus master is none
    esac;

  ------------- bus out values -------------
  bus.AD :=
    case
      bus.arb_gnt = l1_1: cache.AD;
      TRUE: memory.AD;
    esac;
  bus.DT :=
    case
      bus.arb_gnt = l1_1: cache.DT;
      TRUE: memory.DT;
    esac;
  bus.CTRL :=
    case
      bus.arb_gnt = l1_1: cache.CTRL;
      TRUE: memory.CTRL;
    esac;
  bus.valid :=
    case
      bus.arb_gnt = l1_1: cache.valid;
      TRUE: memory.valid;
    esac;

--------------------------- Specifications ---------------------------
 -- there is a branch where mem is infinitely often the master
SPEC EG EF(bus.arb_gnt = mem)
 -- there is a branch where l1_1 is infinitely often the master
SPEC EG EF(bus.arb_gnt = l1_1)


----------------- bus --------------------
-- if l1_1 is bus master, then bus out must be l1_in --
SPEC AG ( (bus.arb_gnt = l1_1) -> (bus.AD = cache.AD & bus.DT = cache.DT & bus.CTRL = cache.CTRL & bus.valid = cache.valid) )
-- if mem is bus master, then bus out must be mem_in --
SPEC AG ( (bus.arb_gnt = mem) -> (bus.AD = memory.AD & bus.DT = memory.DT & bus.valid = memory.valid) )
-- if bus is l1_1 then it should also be valid (not yes respected) --
-- SPEC AG ( (bus.arb_gnt = l1_1) -> (bus.valid) )

---------------- cache -------------------
-- if l1_1 reads data, then next value in bus is the requested value --
SPEC AG ( (bus.valid & bus.arb_gnt = l1_1 & bus.CTRL = read) -> AX AX (bus.DT = memory.data[cache.AD] & memory.valid) )
SPEC EG EF(bus.valid & bus.arb_gnt = l1_1 & bus.CTRL = read)
-- if l1_1 writes data, then next value in memory is the written value --
SPEC AG ( (bus.valid & bus.arb_gnt = l1_1 & bus.CTRL = write) -> AX AX (memory.data[cache.AD] = cache.DT & memory.valid) )
SPEC EG EF(bus.valid & bus.arb_gnt = l1_1 & bus.CTRL = write)

-------------- processor -----------------
SPEC EG EF(processor.valid & processor.req != idle)
-- if proc read a data, the the read value must be the same as the one in memory --
SPEC AG ( (processor.valid & processor.req = load) -> (processor.DT = memory.data[processor.AD]) )
-- if proc read a data, the the read value must be the same as the one in memory --
SPEC AG ( (processor.valid & processor.req = store) -> (processor.REG = memory.data[processor.AD]) )
-- processor can quit idle req state --
SPEC AG ( (processor.req = idle) -> EF (processor.req != idle) )
-- processor can quit load req state --
SPEC AG ( (processor.req = load) -> EF (processor.req != load) )
-- processor can quit write req state --
SPEC AG ( (processor.req = write) -> EF (processor.req != write) )

---------------- other -------------------
-- verify that there are branches whith these cases --
SPEC EG EF(memory.data[0] = TRUE)
SPEC EG EF(memory.data[0] = FALSE)
SPEC EG EF(memory.data[1] = TRUE)
SPEC EG EF(memory.data[1] = FALSE)
SPEC EG EF(cache.AD = 0 & cache.CTRL = read)
SPEC EG EF(cache.AD = 1 & cache.CTRL = read)
SPEC EG EF(cache.AD = 0 & cache.CTRL = write)
SPEC EG EF(cache.AD = 1 & cache.CTRL = write)
SPEC EG EF(cache.is_our_response)
SPEC EG EF(processor.AD = 0 & processor.req = load & processor.REG = TRUE)
SPEC EG EF(processor.AD = 1 & processor.req = load & processor.REG = TRUE)
SPEC EG EF(processor.AD = 0 & processor.req = store & processor.REG = TRUE)
SPEC EG EF(processor.AD = 1 & processor.req = store & processor.REG = TRUE)
SPEC EG EF(processor.AD = 0 & processor.req = load & processor.REG = FALSE)
SPEC EG EF(processor.AD = 1 & processor.req = load & processor.REG = FALSE)
SPEC EG EF(processor.AD = 0 & processor.req = store & processor.REG = FALSE)
SPEC EG EF(processor.AD = 1 & processor.req = store & processor.REG = FALSE)

